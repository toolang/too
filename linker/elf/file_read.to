
use os
use fmt
use io
use string
# 开始解析elf文件
File::readElf(file)
{
	fmt.println("File::readElf ",file)
	fp<i8> = io.fopen(file,"rb")
	if fp <= 0  {
		os.die("file invalid:" + file)
	}
	# 设置游标 开始从头开始读取
	io.rewind(fp)

	# elf文件头部
	ehdr<Elf64_Ehdr> = io.fread(fp,sizeof(Elf64_Ehdr))
	if ehdr.e_type == ET_EXEC {
		io.fseek(fp,ehdr.e_phoff)
		for(i = 0 ; i < int(ehdr.e_phnum); i += 1){
			phdr<Elf64_Phdr> = io.fread(fp,sizeof(Elf64_Phdr))
			this.phdrTab[] = phdr
		}
	}
	io.fseek(fp,ehdr.e_shoff + ehdr.e_shentsize * ehdr.e_shstrndx)
	# 开始读取段表字符串项
	shstrTab<Elf64_Shdr> = io.fread(fp,sizeof(Elf64_Shdr))
	# 转移到段表字符串内容
	io.fseek(fp,shstrTab.sh_offset)
	//读取段表字符串表
	shstrTabData<i8*> = io.fread(fp,shstrTab.sh_size)
	fmt.println("字符串表:",string.new(shstrTabData))

	# 开始读取所有的段表
	io.fseek(fp,ehdr.e_shoff)
	for(i<i32> = 0 ; i < ehdr.e_shnum ; i += 1){
		shdr<Elf64_Shdr> = io.fread(fp,sizeof(Elf64_Shdr))
		# 截取自动遇到\0停止
		name = string.new(shstrTabData + shdr.sh_name)
		fmt.println("read section.name:", name)
		this.shdrNames[] = name
		# map映射
		if name != "" {
			this.shdrTab[name] = shdr
		}
	}
	# 开始读取字符串段表
	strTab<Elf64_Shdr> = this.shdrTab[".strtab"]
	io.fseek(fp,strTab.sh_offset)
	# 读取字符串表
	strTabData<i8*> = io.fread(fp,strTab.sh_size)


	# 读取符号表
	sh_symTab<Elf64_Shdr> = this.shdrTab[".symtab"]
	io.fseek(fp,sh_symTab.sh_size)
	symNum = sh_symTab.sh_size / sh_symTab.sh_entsize

	# 记录符号表段所有符号信息，方便进行重定位
	symList = []
	for(i = 0 ; i < symNum ; i += 1){
		sym<Elf64_Sym> = io.fread(fp,sizeof(Elf64_Sym))
		this.symList[] = sym
		name = string.new(strTabData + sym.st_name)		
		fmt.println("read symbols table.name:", name)
		this.symbols[] = name

		if  name == "_GLOBAL_OFFSET_TABLE_" {
			continue
		}
		if  name != "" {
			this.symTab[name] = sym
			continue
		}
		if  sym.st_shndx != SHN_UNDEF && sym.st_name == 0 {
			# 可能是段名
			name = this.shdrNames[int(sym.st_shndx)]
			fmt.println("read symbols table.name: " ,name)
			if  name != "" {
				this.symTab[name] = sym
			}
		}
	}
	fmt.println(file,"重定位数据")
	# 更新重定位段段数据
	for( k,v : this.shdrTab ){
		if  k == ".rela.text" || k == ".rela.data" {
			relTab<Elf64_Shdr> = v
			io.fseek(fp,relTab.sh_offset)
			relNum = relTab.sh_size / sizeof(Elf64_Rela)
			for(j = 0 ; j < relNum ; j += 1){
				rela<Elf64_Rela> = io.fread(fp,sizeof(Elf64_Rela))
				sym<Elf64_Sym> = symList[ELF64_R_SYM(rela.r_info)]
				index<i32>          = sym.st_name
				name = string.new(strTabData + index)
				fmt.println("name:",name)
				if name == ""{
					if sym.st_shndx != SHN_UNDEF
						name = this.shdrNames[int(sym.st_shndx)]
				}
				this.relTab[] = new RelItem(string.sub(k,5),rela,name)
				fmt.println("name:",name)
			} 
		}
	}
	io.fclose(fp)
}

# 找到符号名对应的索引位置
File::getSymIndex(symname)
{
	index = 0
	//TODO: len() 函数
	sl = std.len(symNames)
	for(i = 0 ; i < sl ; i += 1){
		if  this.symNames[i] == symname {
			break
		}
		index += 1
	}
	return index
}

# 找到段名对应的索引位置
File::getSegIndex(segname)
{
	index = 0
	sl = std.len(this.shdrNames)
	for(i = 0 ; i < sl ; i += 1){
		if   this.shdrNames[i] == segname {
			break
		}
		index += 1
	}
	return index
}

File::getData(buf,offset,size)
{
	# 打开 elf文件	
	fp = io.fopen(file,"rb")
	io.fseek(fp,offset)
	# 读取数据
	buf<i8*> = io.fread(fp,size)
	io.fclose(fp)
}	
